// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or(a=load, b=reset, out=load-or-reset);
    Or(a=load-or-reset, b=inc, out=changed);

    Inc16(in=last-value, out=value-plus-one);

    Mux16(a=last-value, b=value-plus-one, sel=inc, out=inc-value);
    Mux16(a=inc-value, b=in, sel=load, out=load-value);
    Mux16(a=load-value, b=false, sel=reset, out=reset-value);

    Register(in=reset-value, load=changed, out=last-value, out=out);
}
