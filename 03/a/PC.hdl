// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=previous-value, out=inc-out);
    Mux16(a=previous-value, b=inc-out, sel=inc, out=inc-mux-out);
    Mux16(a=inc-mux-out, b=in, sel=load, out=load-mux-out);
    Mux16(a=load-mux-out, b=false, sel=reset, out=reset-mux-out);
    Register(in=reset-mux-out, load=true, out=out, out=previous-value);
}
